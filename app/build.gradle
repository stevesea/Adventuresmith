apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'groovyx.android'
apply plugin: 'com.neenbedankt.android-apt' // must be after groovy plugin
apply plugin: "org.sonarqube"
apply plugin: 'io.fabric'
//apply plugin: 'easyrelease'

apply from: "$rootDir/gradle/signing.gradle"

androidGitVersion {
    // default:
    //format = '%tag%%.count%%<commit>%%-branch%%...dirty%'
    format = '%tag%%.count%%-commit%%-branch%'
}

ext {
    groovyVersion = '2.4.7'
    androidSupportVersion = '24.2.0'
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "org.steavesea.adventuresmith"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file("$keystorePath")
            storePassword "$keystorePassword"
            keyAlias "$storeKeyAlias"
            keyPassword "$aliasKeyPassword"
        }
    }
    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 16
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            debuggable true
            applicationIdSuffix
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        warning 'InvalidPackage' // lint sees references in the grooid jar to unavailable java classes
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/org.codehaus.groovy.runtime.ExtensionModule'
        exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
        exclude 'META-INF/groovy-release-info.properties'
        exclude 'META-INF/LICENSE'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

androidGroovy {
    // setup custom groovy compile options
    options {
        configure(groovyOptions) {
            javaAnnotationProcessing = true
            configurationScript = file("$projectDir/config/groovy-compile-options.groovy")
        }
    }
    // skip javac MUST be false for not set for apt to work correctly
}

sonarqube {
    properties {
        property "sonar.projectName", "Adventuresmith :: Simple Project :: Generate nonsense at push of a button!"
        property "sonar.projectKey", "org.stevesea:adventuresmith"

        property "sonar.sourceEncoding","UTF-8"
        property "sonar.sources","."
        property "sonar.exclusions","build/**,**/*.png"

        property "sonar.import_unknown_files", true

        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
    }
}

dependencies {
    compile project(':adventuresmith-core')
    compile 'com.android.support:multidex:1.0.1'

    compile "org.codehaus.groovy:groovy:$groovyVersion:grooid"
    // the following dependency is necessary if you want JSON support
    compile ("org.codehaus.groovy:groovy-json:$groovyVersion") {
        exclude group: 'org.codehaus.groovy'
    }

    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.samskivert:jmustache:1.13'

    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    // used to provide out of the box icon font support. simplifies development,
    // and provides scalable icons. the core is very very light
    // https://github.com/mikepenz/Android-Iconics
    compile 'com.mikepenz:iconics-core:2.8.0@aar'
    compile 'com.mikepenz:community-material-typeface:1.5.54.2@aar'
    compile 'com.mikepenz:ionicons-typeface:2.0.1.2@aar'

    // note: this brings in fastadapter
    compile('com.mikepenz:materialdrawer:5.6.0@aar') {
        transitive = true
    }
    compile "com.mikepenz:materialize:1.0.0@aar"

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    testCompile "junit:junit:$junit_version"
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "org.robolectric:robolectric:$robolectric_version"
    testCompile "org.robolectric:shadows-multidex:$robolectric_version"
    //testCompile "org.robolectric:shadows-support-v4:3.0"

    compile 'com.github.salomonbrys.kodein:kodein-android:3.1.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"
}

// APT doesn't know to make these directories for tests. Force them to
// be written.
afterEvaluate {
    def createAptTestDirs = tasks.create('createAptTestDirs')
    createAptTestDirs.doFirst {
        file("$buildDir/generated/source/apt/test/dev/debug").mkdirs()
        file("$buildDir/generated/source/apt/test/dev/release").mkdirs()
        file("$buildDir/generated/source/apt/test/prod/debug").mkdirs()
        file("$buildDir/generated/source/apt/test/prod/release").mkdirs()
    }
    tasks.getByName('mockableAndroidJar').finalizedBy(createAptTestDirs)
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
    }
}
repositories {
    mavenCentral()
}
