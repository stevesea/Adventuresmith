apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//apply plugin: 'easyrelease'

try {
    // wrap with try so build works even if signing stuff is missing
    apply from: "../../advsmith_signing.gradle"

    // now part of ../../advsmith_signing
//    signingConfigs {
//        debug {
//            storeFile file('debug.keystore')
//            storePassword 'android'
//            keyAlias 'androiddebugkey'
//            keyPassword 'android'
//        }
//        release {
//            storeFile file("$keystorePath")
//            storePassword "$keystorePassword"
//            keyAlias "$storeKeyAlias"
//            keyPassword "$aliasKeyPassword"
//        }
//    }
} catch (ex) {

}


androidGitVersion {
    // default:
    //format = '%tag%%.count%%<commit>%%-branch%%...dirty%'
    format = '%tag%%.count%%-commit%%-branch%'
}

ext {
    androidSupportVersion = '28.0.0'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "org.steavesea.adventuresmith"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        // Enable VectorDrawableCompat for API < 21
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "minApi"
    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
            dimension "minApi"
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 16
            dimension "minApi"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            debuggable true
            applicationIdSuffix
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            try {
                signingConfig signingConfigs.release
            } catch (ex) {

            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError true
        textReport true
        textOutput 'stdout'
        warning 'InvalidPackage' // lint sees references in the grooid jar to unavailable java classes
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation project(':adventuresmith-core')
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.github.salomonbrys.kodein:kodein-android:$kodein_version"
    implementation "io.github.microutils:kotlin-logging:$kotlin_logging_version"
    implementation "com.github.salomonbrys.kodein:kodein:$kodein_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"

    // used to provide out of the box icon font support. simplifies development,
    // and provides scalable icons. the core is very very light
    // https://github.com/mikepenz/Android-Iconics
    implementation "com.mikepenz:iconics-core:$iconics_core_version"
    implementation "com.mikepenz:iconics-views:$iconics_core_version"
    implementation "com.mikepenz:community-material-typeface:$iconics_cmd_version"
    implementation "com.mikepenz:ionicons-typeface:$iconics_ion_version"

    implementation("com.mikepenz:materialdrawer:$materialdrawer_version") {
        transitive = true
        exclude module: "fastadapter"
    }

    implementation "com.mikepenz:fastadapter:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-commons:$fastadapter_commons_version"
    implementation "com.mikepenz:fastadapter-extensions:$fastadapter_extensions_version"

    implementation "com.mikepenz:materialize:$materialize_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "org.jetbrains.anko:anko-sdk23:$ankoVersion" // sdk19, sdk21, sdk23 are also available
    implementation "org.jetbrains.anko:anko-support-v4:$ankoVersion" // In case you need support-v4 bindings
    implementation "org.jetbrains.anko:anko-appcompat-v7:$ankoVersion" // For appcompat-v7 bindings

    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"


    implementation group: 'org.slf4j', name: 'slf4j-android', version: slf4j_version
}

// APT doesn't know to make these directories for tests. Force them to
// be written.
afterEvaluate {
    def createAptTestDirs = tasks.create('createAptTestDirs')
    createAptTestDirs.doFirst {
        file("$buildDir/generated/source/apt/test/dev/debug").mkdirs()
        file("$buildDir/generated/source/apt/test/dev/release").mkdirs()
        file("$buildDir/generated/source/apt/test/prod/debug").mkdirs()
        file("$buildDir/generated/source/apt/test/prod/release").mkdirs()
    }
    tasks.getByName('createMockableJar').finalizedBy(createAptTestDirs)
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
    }
}

check.dependsOn detekt
